plugins {
    id 'net.ltgt.errorprone'  version '4.3.0' apply false
    id 'com.diffplug.spotless' version '8.0.0' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        groovyGradle {
            target '**/*.gradle'
            targetExclude 'build/**', '.gradle/**', '.direnv/**'
            greclipse()
            leadingTabsToSpaces(4)
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    plugins.withId('java') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(25)
            }
        }

        spotless {
            java {
                googleJavaFormat('1.32.0')
            }
        }

        apply plugin: 'net.ltgt.errorprone'
        dependencies {
            errorprone("com.google.errorprone:error_prone_core:2.44.0")
        }
    }

    apply plugin: 'eclipse'
    eclipse.classpath {
        defaultOutputDir = file("build/default")
        file.whenMerged { cp ->
            cp.entries.forEach { cpe ->
                if (cpe instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
                    cpe.output = cpe.output.replace "bin/", "build/classes/java/"
                }
                if (cpe instanceof org.gradle.plugins.ide.eclipse.model.Output) {
                    cpe.path = cpe.path.replace "bin/", "build/"
                }
            }
        }
    }
}
